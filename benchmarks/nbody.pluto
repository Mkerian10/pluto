class Body {
    x: float
    y: float
    z: float
    vx: float
    vy: float
    vz: float
    mass: float
}

fn advance(bodies: [Body], dt: float) {
    let n = bodies.len()
    let mut i = 0
    while i < n {
        let mut bi = bodies[i]
        let mut j = i + 1
        while j < n {
            let mut bj = bodies[j]
            let dx = bi.x - bj.x
            let dy = bi.y - bj.y
            let dz = bi.z - bj.z
            let dist2 = dx * dx + dy * dy + dz * dz
            let dist = sqrt(dist2)
            let mag = dt / (dist2 * dist)

            bi.vx = bi.vx - dx * bj.mass * mag
            bi.vy = bi.vy - dy * bj.mass * mag
            bi.vz = bi.vz - dz * bj.mass * mag

            bj.vx = bj.vx + dx * bi.mass * mag
            bj.vy = bj.vy + dy * bi.mass * mag
            bj.vz = bj.vz + dz * bi.mass * mag
            j = j + 1
        }
        i = i + 1
    }

    i = 0
    while i < n {
        let mut b = bodies[i]
        b.x = b.x + dt * b.vx
        b.y = b.y + dt * b.vy
        b.z = b.z + dt * b.vz
        i = i + 1
    }
}

fn energy(bodies: [Body]) float {
    let n = bodies.len()
    let mut e = 0.0
    let mut i = 0
    while i < n {
        let bi = bodies[i]
        e = e + 0.5 * bi.mass * (bi.vx * bi.vx + bi.vy * bi.vy + bi.vz * bi.vz)
        let mut j = i + 1
        while j < n {
            let bj = bodies[j]
            let dx = bi.x - bj.x
            let dy = bi.y - bj.y
            let dz = bi.z - bj.z
            let dist = sqrt(dx * dx + dy * dy + dz * dz)
            e = e - bi.mass * bj.mass / dist
            j = j + 1
        }
        i = i + 1
    }
    return e
}

fn main() {
    let pi = 3.141592653589793
    let solar_mass = 4.0 * pi * pi
    let days_per_year = 365.24

    // Sun
    let mut sun = Body {
        x: 0.0, y: 0.0, z: 0.0,
        vx: 0.0, vy: 0.0, vz: 0.0,
        mass: solar_mass
    }

    // Jupiter
    let jupiter = Body {
        x: 4.84143144246472090,
        y: -1.16032004402742839,
        z: -0.103622044471123109,
        vx: 0.00166007664274403694 * days_per_year,
        vy: 0.00769901118419740425 * days_per_year,
        vz: -0.0000690460016972063023 * days_per_year,
        mass: 0.000954791938424326609 * solar_mass
    }

    // Saturn
    let saturn = Body {
        x: 8.34336671824457987,
        y: 4.12479856412430479,
        z: -0.403523417114321381,
        vx: -0.00276742510726862411 * days_per_year,
        vy: 0.00499852801234917238 * days_per_year,
        vz: 0.0000230417297573763929 * days_per_year,
        mass: 0.000285885980666130812 * solar_mass
    }

    // Uranus
    let uranus = Body {
        x: 12.8943695621391310,
        y: -15.1111514016986312,
        z: -0.223307578892655734,
        vx: 0.00296460137564761618 * days_per_year,
        vy: 0.00237847173959480950 * days_per_year,
        vz: -0.0000296589568540237556 * days_per_year,
        mass: 0.0000436624404335156298 * solar_mass
    }

    // Neptune
    let neptune = Body {
        x: 15.3796971148509165,
        y: -25.9193146099879641,
        z: 0.179258772950371181,
        vx: 0.00268067772490389322 * days_per_year,
        vy: 0.00162824170038242295 * days_per_year,
        vz: -0.0000951592254519715870 * days_per_year,
        mass: 0.0000515138902046611451 * solar_mass
    }

    let bodies = [sun, jupiter, saturn, uranus, neptune]

    // Offset momentum
    let mut px = 0.0
    let mut py = 0.0
    let mut pz = 0.0
    let mut i = 0
    while i < bodies.len() {
        let b = bodies[i]
        px = px + b.vx * b.mass
        py = py + b.vy * b.mass
        pz = pz + b.vz * b.mass
        i = i + 1
    }
    sun.vx = -px / solar_mass
    sun.vy = -py / solar_mass
    sun.vz = -pz / solar_mass

    let steps = 50000000
    let start = time_ns()

    let e0 = energy(bodies)
    print("energy before: {e0}")

    i = 0
    while i < steps {
        advance(bodies, 0.01)
        i = i + 1
    }

    let e1 = energy(bodies)
    print("energy after: {e1}")

    let elapsed = time_ns() - start
    let ms = elapsed / 1000000
    print("elapsed: {ms} ms")
}
